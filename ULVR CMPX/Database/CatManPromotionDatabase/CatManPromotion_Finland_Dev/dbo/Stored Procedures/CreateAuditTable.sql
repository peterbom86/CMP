CREATE PROCEDURE CreateAuditTable
  @name NVARCHAR(255)

AS

DECLARE @PrimaryKey NVARCHAR(255)
DECLARE @ForeignKey NVARCHAR(255)

DECLARE @sql NVARCHAR(4000)

SELECT @PrimaryKey = KCU.COLUMN_NAME, @ForeignKey = 'FK_' + KCU.COLUMN_NAME
FROM INFORMATION_SCHEMA.TABLE_CONSTRAINTS TC
  INNER JOIN INFORMATION_SCHEMA.KEY_COLUMN_USAGE KCU ON TC.TABLE_SCHEMA = KCU.TABLE_SCHEMA AND
    TC.CONSTRAINT_NAME = KCU.CONSTRAINT_NAME
WHERE TC.CONSTRAINT_TYPE = 'PRIMARY KEY' AND TC.TABLE_SCHEMA = 'dbo' AND TC.TABLE_NAME = @name
  
IF (SELECT COUNT(*) FROM sysobjects WHERE xtype = 'U' AND name = @name + '_Audit') > 0
BEGIN
  --SET @sql = ''

  SELECT @sql = ISNULL(@sql + ', 
  ', 'ALTER TABLE ' + @name + '_Audit
ADD ') + c1.COLUMN_NAME + ' ' + DATATYPE FROM 
  ( SELECT COLUMN_NAME, DATA_TYPE + ISNULL('(' + CAST(CHARACTER_MAXIMUM_LENGTH AS NVARCHAR) + ')', '') DATATYPE FROM INFORMATION_SCHEMA.COLUMNS WHERE TABLE_NAME = @name ) c1
    LEFT JOIN ( SELECT COLUMN_NAME FROM INFORMATION_SCHEMA.COLUMNS WHERE TABLE_NAME = @name + '_Audit' ) c2 ON REPLACE(REPLACE(c1.COLUMN_NAME, 'PK_', ''), 'FK_', '') = REPLACE(c2.COLUMN_NAME, 'FK_', '')
  WHERE c2.COLUMN_NAME IS NULL
  
  --ALTER TABLE
END
ELSE
BEGIN
  --CREATE TABLE 
  SET @sql = 'CREATE TABLE ' + @name + '_Audit (
  AuditID int IDENTITY(1, 1) PRIMARY KEY, 
  '
  
  SELECT @sql = @sql + 
    CASE WHEN COLUMN_NAME = @PrimaryKey THEN 'FK_' + REPLACE(COLUMN_NAME, 'PK_', '') ELSE COLUMN_NAME END + 
  ' ' + DATA_TYPE + ISNULL('(' + CAST(CHARACTER_MAXIMUM_LENGTH AS NVARCHAR) + ')', '') + ',
  '
  FROM INFORMATION_SCHEMA.COLUMNS WHERE TABLE_NAME = @name
  
  SET @sql = @sql + 'AuditType nvarchar(10),
  AuditDate datetime DEFAULT(GETDATE()),
  AuditBy nvarchar(50) DEFAULT(SYSTEM_USER))'
END

IF ISNULL(@sql, '') <> ''
EXEC ( @sql )
